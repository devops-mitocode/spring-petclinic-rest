pipeline {
    agent none
    environment {
        DTRACK_BASE_URL = 'http://34.216.52.146:9091'
        DTRACK_API_KEY  = credentials('dependencytrack-token')
    }
    stages {
        stage('Build Artifact') {
            agent {
                docker {
                    image 'maven:3.9.9-eclipse-temurin-17-alpine'
                }
            }
            steps {
                sh 'mvn -U -B -ntp -DskipTests clean package'
            }
        }
        stage('Build Image') {
            agent {
                docker {
                    image 'docker:29.1.3-cli-alpine3.23'
                    args '-u root:root -v /var/run/docker.sock:/var/run/docker.sock'
                }
            }
            options {
                skipDefaultCheckout()
            }
            steps {

                script {
                    def pom = readMavenPom file: 'pom.xml'
                    env.ARTIFACT_ID = pom.artifactId
                    env.VERSION     = pom.version
                    env.IMAGE     = "${env.ARTIFACT_ID}:${env.VERSION}"
                }

                sh 'docker build --pull -t "$IMAGE" .'
            }
        }
        stage('Scan Image (Generate SBOM)') {
            agent {
                docker {
                    image 'aquasec/trivy:0.68.2'
                    args "--entrypoint='' -u root:root -v /var/run/docker.sock:/var/run/docker.sock"
                }
            }
            options { 
                skipDefaultCheckout()
            }
            environment {
                TRIVY_DISABLE_VEX_NOTICE = 'true'
            }
            steps {
                sh '''
                    # Genera SBOM CycloneDX desde la imagen Docker usando `trivy image`.
                    # Nota: este modo es vulnerability-scan first; el SBOM es un formato de salida.
                    trivy image "$IMAGE" \
                        --format cyclonedx \
                        --output trivy-image-bom.json \
                        --no-progress

                    ls -lh trivy-image-bom.json
                '''

                dependencyTrackPublisher(
                    artifact: 'trivy-image-bom.json',
                    projectName: "${env.ARTIFACT_ID}-container",
                    projectVersion: env.VERSION,
                    dependencyTrackUrl: env.DTRACK_BASE_URL,
                    dependencyTrackApiKey: env.DTRACK_API_KEY,
                    autoCreateProjects: true,
                    synchronous: true
                )
            }
            post {
                always {
                    archiveArtifacts artifacts: 'trivy-image-bom.json', fingerprint: true
                }
            }
        }
    }
}